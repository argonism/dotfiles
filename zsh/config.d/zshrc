#
# .zshrc - The zsh configuration
#


export USER=$(whoami)

export LANG=en_US.UTF-8
export LC_CTYPE=en_US.UTF-8

export EDITOR=vim
export XDG_CONFIG_HOME=$HOME/.config

export PATH=$PATH:$HOME/.dotfiles/bin
export PATH=$HOME/.local/bin:$PATH

export PATH=$PATH:/usr/texbin
export PATH=/usr/local/bin:$PATH
export PATH=/usr/local/sbin:$PATH

# pyenv configs
export PYENV_ROOT=$HOME/.pyenv
export PATH="$PYENV_ROOT/bin:$PATH"
export PYTHONDONTWRITEBYTECODE=1

# path to system python for NeoVim
if ! [[ -v SYSTEM_PYTHON_PATH ]]; then
  export SYSTEM_PYTHON_PATH=`which python3`
fi


# generals
case ${OSTYPE} in
  darwin*)
    # for brew
    export PATH=/opt/brew/bin:$PATH
    ;;

  linux*)
    # for ipython
    export QT_QPA_PLATFORM="offscreen"

    # for brew
    export PATH=$HOME/.linuxbrew/bin:$PATH
    export PATH=$HOME/.linuxbrew/sbin:$PATH
    export PATH=/home/linuxbrew/.linuxbrew/bin:$PATH
    export PATH=/home/linuxbrew/.linuxbrew/sbin:$PATH

    # for GPU
    export PATH=/usr/local/cuda/bin:$PATH
    export LD_LIBRARY_PATH=~/.cudnn/active/cuda/lib64:$LD_LIBRARY_PATH
    export CPATH=~/.cudnn/active/cuda/include:$CPATH
    export LIBRARY_PATH=~/.cudnn/active/cuda/lib64:$LIBRARY_PATH
    ;;
esac


# home dirs
if builtin command -v brew > /dev/null; then
  export BREW_HOME=$(brew --prefix)
  # NOTE: If brew is installed, we use Python installed by brew as a SYSTEM_PYTHON_PATH
  export SYSTEM_PYTHON_PATH=$(brew --prefix)/bin/python3
fi


# llvm
export PATH=$BREW_HOME/opt/llvm/bin:$PATH
export PATH=$BREW_HOME/opt/llvm/bin:$PATH
export LDFLAGS="-L$BREW_HOME/opt/llvm/lib"
export CPPFLAGS="-I$BREW_HOME/opt/llvm/include"


# golang
export GOPATH=$HOME/go
export PATH=$GOPATH/bin:$PATH


# mecab
case ${OSTYPE} in
  darwin*)
    export MECAB_PATH=$BREW_HOME/lib/libmecab.dylib
    ;;

  linux*)
    if builtin command -v mecab-config > /dev/null; then
      export MECAB_PATH=`mecab-config --libs-only-L`/libmecab.so.2
    fi
    ;;

esac


# aliases
case ${OSTYPE} in
  darwin*)
    alias ls="ls -G -F"
    ;;
  linux*)
    alias ls="ls --color=auto"
    if builtin command -v xsel > /dev/null; then
      alias pbcopy='xsel --clipboard --input'
      alias pbpaste='xsel --clipboard --output'
    fi
    ;;
esac

alias l="ls"
alias la="ls -a"
alias lla="ls -la"
alias zmv="noglob zmv -W"


# zplug
export ZPLUG_HOME=$HOME/.config/zplug

if [ -e $ZPLUG_HOME/init.zsh ]; then
  source $ZPLUG_HOME/init.zsh
  zplug "himkt/zsh-git-prompt", use:"zsh-git-prompt.zsh"
  zplug "zsh-users/zsh-history-substring-search"
  zplug "zsh-users/zsh-syntax-highlighting", defer:2
  zplug "rupa/z", use:z.sh
  zplug "stedolan/jq", from:gh-r, as:command, rename-to:jq
  zplug "junegunn/fzf-bin", from:gh-r, as:command, rename-to:fzf

  # check whether if there are packages which it can install
  if ! zplug check --verbose; then
    zplug install
  fi

  zplug load
fi


# etc
if which tmux > /dev/null; then
  if [ -z $TMUX ] && [ $TERM_PROGRAM != "vscode" ]; then
    if $(tmux has-session); then
      tmux attach
    else
      tmux
    fi
  fi
fi


if which rbenv > /dev/null; then
  eval "$(rbenv init -)"
fi

if which pyenv > /dev/null; then
  eval "$(pyenv init -)"
fi

if which nvim > /dev/null; then
  alias vim=nvim
  export EDITOR=nvim
fi

if which pyenv-virtualenv-init > /dev/null; then
  eval "$(pyenv virtualenv-init -)"
fi

if which ghq > /dev/null; then
  export GHQ_ROOT=$HOME/work
fi


# autoloads
autoload -Uz zmv
autoload -Uz add-zsh-hook
autoload -Uz is-at-least
autoload -U compinit; compinit


# zstyles
zstyle ":completion:*" matcher-list "m:{a-z}={A-Z}"
zstyle ":completion:*:default" menu select=1


# options
setopt auto_cd correct
setopt APPEND_HISTORY INC_APPEND_HISTORY SHARE_HISTORY
setopt extended_history share_history
setopt hist_ignore_all_dups HIST_IGNORE_SPACE
setopt auto_param_slash
setopt mark_dirs
setopt list_types
setopt auto_menu
setopt auto_param_keys
setopt interactive_comments
setopt magic_equal_subst
setopt complete_in_word
setopt print_eight_bit
setopt extended_glob
setopt globdots
setopt auto_list
setopt no_beep
setopt no_flow_control
setopt magic_equal_subst
setopt auto_menu
setopt extended_glob


# colorscheme
if [ "$COLORTERM" = "gnome-terminal" ] || [ "$COLORTERM" = "xfce4-terminal" ]; then
  export TERM=xterm-256color
elif [ "$COLORTERM" = "rxvt-xpm" ]; then
  export TERM=rxvt-256color
fi


# custom functions using fzf
zle -N fzf-change-dir
zle -N fzf-select-history
zle -N fzf-z-search
zle -N fzf-open-editor


# bindkeys
bindkey -e
bindkey '^t' fzf-change-dir
bindkey '^r' fzf-select-history
bindkey '^s' fzf-z-search


# history
HISTFILE=$HOME/.zsh-history
HISTSIZE=100000
SAVEHIST=100000


# pipenv
function detect_pipenv() {
  local pipenv_pattern="(pipenv) \\$ "
  local normal_pattern="\\$ "

  if [[ -f ./Pipfile ]] ; then
    alias  python="pipenv run python"
    alias  pip="pipenv run pip"
    export PIPENV_ROOT=$(pwd)

    if [[ $PROMPT =~ "pipenv" ]] ; then
      return
    fi
    export PROMPT=`echo $PROMPT| sed "s/$normal_pattern/$pipenv_pattern/g"`
  else
    if ! [[ -v PIPENV_ROOT ]] ; then
      # noop
    else
      if ! [[ $(pwd) =~ $PIPENV_ROOT ]] ; then
        unalias python
        unalias pip
        unset   PIPENV_ROOT
        export  PROMPT=`echo $PROMPT| sed "s/$pipenv_pattern/$normal_pattern/g"`
      fi
    fi
  fi
}

function cd() {
  builtin cd $@
  detect_pipenv
  ls
}

function ssh() {
  if [ "$(ps -p $(ps -p $$ -o ppid=) -o comm=)" = "tmux" ]; then
    tmux rename-window ${@: -1}
    command ssh "$@"
    tmux set-window-option automatic-rename "on" 1>/dev/null
  else
    command ssh "$@"
  fi
}

function fzf-change-dir() {
  local target
  target=$(find ${1:-.} -path '*/\.*' -prune -o -type d -print 2> /dev/null | fzf +m) && cd $target
}

function fzf-select-history() {
  BUFFER=$(history -n -r 1 | fzf --no-sort +m --query "$LBUFFER" --prompt="History > ")
  CURSOR=$#BUFFER
}

function fzf-z-search() {
    local res=$(z | sort -rn | cut -c 12- | fzf)
    if [ -n "$res" ]; then
        BUFFER+="cd $res"
        zle accept-line
    else
        return 1
    fi
}

cd .  # For venv, cd runs here
